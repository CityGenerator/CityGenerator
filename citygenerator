#!/usr/bin/perl -wT

use strict;
use CGI;
use Data::Dumper;
use JSON;
use List::Util 'shuffle', 'min', 'max' ;
use POSIX;
use Template;
use Template::Plugin::Lingua::EN::Inflect; # This is used within the Templates
use XML::Simple;
use lib "lib/";
use GenericGenerator;
use FlagGenerator;
use CityGenerator;
use SummaryFormatter;
use EnvironmentFormatter;
use CityscapeFormatter;
use CensusDataFormatter;
use GovtFormatter;
use CultureFormatter;
use LocaleFormatter;
use MapJSONFormatter;
use PeopleFormatter;
use EconomyFormatter;
use EventFormatter;
my $q = CGI->new;


    my $city_presets=parse_presets($q);

    my $city=CityGenerator::create($city_presets);
    CityGenerator::flesh_out_city($city);
    $city->{'flag'}=FlagGenerator::create({seed=>$city->{'seed'}, 'cityname'=>$city->{'name'}});
if (defined $q->param('type') and $q->param('type') eq 'xml' ){
    print $q->header( 'text/xml' );
    print "<?xml version='1.0'  encoding='utf-8' ?>\n";
    print XMLout($city);
}elsif (defined $q->param('type') and $q->param('type') eq 'json' ){
    print $q->header( 'application/json' );
    my $JSON  = JSON->new->utf8;
    $JSON->convert_blessed(1);
    print  $JSON->encode($city);

}elsif (defined $q->param('type') and $q->param('type') eq 'dump' ){
    print $q->header( 'text/plain' );
    print Dumper $city;
}elsif (defined $q->param('type') and $q->param('type') eq 'post' ){
    print $q->header( 'text/plain' );
    print Dumper $city_presets;
}else {
    print $q->header( 'text/html; charset=utf-8' );
    print_page($city);
}

exit;

#################################################################################################
#################################################################################################
#################################################################################################
sub parse_presets {
    my ($q)=@_;
    my $presets;
    $presets->{'seed'}                  = $1 if ( defined $q->param('seed')            and $q->param('seed')              =~/^(\d+)$/x ); 
    $presets->{'govt'}->{'seed'}        = $1 if ( defined $q->param('govtseed')        and $q->param('govtseed')          =~/^(\d+)$/x ); 
    $presets->{'military'}->{'seed'}    = $1 if ( defined $q->param('militaryseed')    and $q->param('militaryseed')      =~/^(\d+)$/x ); 
    $presets->{'climate'}->{'seed'}     = $1 if ( defined $q->param('climateseed')     and $q->param('climateseed')       =~/^(\d+)$/x ); 
    $presets->{'size_roll'}             = $1 if ( defined $q->param('size_roll')       and $q->param('size_roll')         =~/^(\d+)$/x );
    $presets->{'children'}->{'precent'} = $1 if ( defined $q->param('child_precent')   and $q->param('child_precent')     =~/^(\d+)$/x );
    $presets->{'elderly'}->{'percent'}  = $1 if ( defined $q->param('elderly_percent') and $q->param('elderly_percent')   =~/^(\d+)$/x );

    foreach my $stat ( ( 'moral', 'order','education','magic','military','tolerance','economy','authority' ) ){
        $presets->{'stats'}->{$stat} = $1 if ( defined $q->param($stat)     and $q->param($stat)       =~/^(\d+)$/x ); 
    }

    foreach my $climatestat ( ('altitude', 'continentality', 'latitude', 'pressure', 'temperature', 'precipitation' ) ){
        $presets->{'climate'}->{'stats'}->{$climatestat} = $1 if ( defined $q->param($climatestat)     and $q->param("climate_".$climatestat)       =~/^(\d+)$/x ); 
    }

    return $presets;

}

sub print_page {
    my ($city)=@_;

    my $templates = Template->new({
        INCLUDE_PATH => 'Templates/',
        INTERPOLATE  => 1,
    }) || die "$Template::ERROR\n";
    my $JSON  = JSON->new->utf8;

    $JSON->convert_blessed(1);
    my $json=$JSON->encode($city->{'flag'});

    my $vars = {
        'title'         => 'Welcome to '.$city->{'name'}."!",
        'logo'          => "<canvas id='flag' style='' width='180' height='90'  onclick='embiggen(this);'  > </canvas>",
        'seed'          => "$city->{'seed'}",
        'onload'        => "create_flag( $json,  \"flag\");",

        'content'       => print_content($city),
        'worldnav'      => print_worldnav($city),
    };
    $templates->process('base.tmpl', $vars) || die $templates->error(), "\n";
}

sub print_worldnav {
    my ($city)=@_;

    my $templates = Template->new({
        INCLUDE_PATH => 'Templates/',
        INTERPOLATE  => 1,
    }) || die "$Template::ERROR\n";

    my $formtmpl;
    my $vars = {
        'cityname'      => $city->{'name'},
        'seed'          => $city->{'seed'},
    };
    $templates->process('worldnav.tmpl', $vars, \$formtmpl) || die $templates->error(), "\n";

    return $formtmpl;
}


sub print_content {
    my ($city)=@_;

    my $templates = Template->new({
        INCLUDE_PATH => 'Templates/',
        INTERPOLATE  => 1,
    }) || die "$Template::ERROR\n";

    my $formtmpl;
    my $vars = {
    #TODO come up with a better, cleaner way to do this. Waaay too manual.
        'cityname'      => $city->{'name'},
        'seed'          => $city->{'seed'},
        'summarytext'   => SummaryFormatter::printSummary($city),

        'citymapjson'   => MapJSONFormatter::printCityMapJSON($city),

        'geographytext' => EnvironmentFormatter::printGeography($city),
        'climatetext'   => EnvironmentFormatter::printClimate($city),
        'astronomytext' => EnvironmentFormatter::printAstronomy($city),

        'censusdatatext'=> CensusDataFormatter::printCensusData($city),

        'wallstext'     => CityscapeFormatter::printWalls($city),
        'streetstext'   => CityscapeFormatter::printStreets($city),
        'districtstext'  => CityscapeFormatter::printDistricts($city),

        'govtsummarytext'   => GovtFormatter::printSummary($city),
        'govtleadertext'    => GovtFormatter::printLeader($city),
        'govtlawstext'      => GovtFormatter::printLaw($city),
        'govtcrimetext'     => GovtFormatter::printCrime($city),
        'govtmilitarytext'  => GovtFormatter::printMilitary($city),

        'eventstext'        => EventFormatter::printSummary($city),
        'postingstext'      => EventFormatter::printPostings($city),

        'religionstext'     => CultureFormatter::printReligions($city),
        'legendstext'       => CultureFormatter::printLegends($city),

        'peopletext'        => PeopleFormatter::printSummary($city),
        'citizenstext'      => PeopleFormatter::printCitizens($city),
        'travelerstext'     => PeopleFormatter::printTravelers($city),

        'economytext'       => EconomyFormatter::printSummary($city),
        'currenciestext'    => EconomyFormatter::printCurrencies($city),
        'resourcestext'     => EconomyFormatter::printResources($city),
        'businessestext'    => EconomyFormatter::printBusinesses($city),

        'establishmentstext' => LocaleFormatter::printEstablishments($city),

    };
    $templates->process('citygenerator_content.tmpl', $vars, \$formtmpl) || die $templates->error(), "\n";

    return $formtmpl;
}

__END__


=head1 AUTHOR

Jesse Morgan (morgajel)  C<< <morgajel@gmail.com> >>

=head1 LICENSE AND COPYRIGHT

Copyright (c) 2013, Jesse Morgan (morgajel) C<< <morgajel@gmail.com> >>. All rights reserved.

This program is free software; you can redistribute it and/or
modify it under the terms of the GNU General Public License
as published by the Free Software Foundation version 2
of the License.

You should have received a copy of the GNU General Public License
along with this program; if not, write to the Free Software
Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA  02110-1301, USA.

=head1 DISCLAIMER OF WARRANTY

This program is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU General Public License for more details.

=cut
